USE TEMPDB
GO

--主控过程:
--参数说明：两个参数为两个酒鬼ID
--调用示例: EXEC GAME_HUAQUAN 'zhangsan', 'lisi'

IF(OBJECT_ID('GAME_HUAQUAN') IS NOT NULL) 
DROP PROCEDURE GAME_HUAQUAN
GO
CREATE PROCEDURE GAME_HUAQUAN (@EASTID NVARCHAR(100),@WESTID NVARCHAR(100))
AS
BEGIN
	--出拳和输出的动态SQL语句变量
	DECLARE @EAST_COME NVARCHAR(1000),@WEST_COME NVARCHAR(1000)
	DECLARE @EAST_COMEOUT NVARCHAR(1000),@WEST_COMEOUT NVARCHAR(1000)
	--两酒鬼的出拳和所叫数值定义
	DECLARE @EAST_FINGER INT,@EAST_SUM INT,@WEST_FINGER INT,@WEST_SUM INT
	--分数定义
	DECLARE @EAST_SCORE INT, @WEST_SCORE INT
	--结果定义
	DECLARE @EAST_RESULT NVARCHAR(10) , @WEST_RESULT NVARCHAR(10)
	--超时控制变量定义
	DECLARE @EAST_TIME INT ,@WEST_TIME INT,@EAST_TIMESTART DATETIME,@WEST_TIMESTART DATETIME,
			@EAST_TIMECOUNT INT, @WEST_TIMECOUNT INT
	--循环定义
	DECLARE @I INT,@J INT
	DECLARE @BREAK_J INT, @BREAK_I INT
	
	--初始化定义变量
	SET @EAST_TIMECOUNT=0 SET @WEST_TIMECOUNT=0
	SET @EAST_SCORE=0 SET @WEST_SCORE=0
	SET @BREAK_J=0 SET @BREAK_I=0
	
	SET @I=0;SET @J=0
     
	--初始化两酒鬼
	SET @EAST_TIMESTART=GETDATE()
	EXEC (@EASTID+'_INIT')
	SET @EAST_TIME=DATEDIFF(MS,@EAST_TIMESTART,GETDATE())
	
	SET @WEST_TIMESTART=GETDATE()
	EXEC (@WESTID+'_INIT')
	SET @WEST_TIME=DATEDIFF(MS,@WEST_TIMESTART,GETDATE())

	--初始化超时判断
	IF(@EAST_TIME>1000 OR @WEST_TIME>1000)
		SET @BREAK_I=1
	
	--开始比赛
	WHILE(@I<1000 AND @BREAK_I=0)
	BEGIN
		SET @BREAK_J=0
		SET @J=0
		WHILE(@J<100 AND @BREAK_J=0)
		BEGIN
			SET @EAST_RESULT='未知' SET @WEST_RESULT='未知'
			--东家酒鬼出拳
			SET @EAST_COME='EXEC '+@EASTID+'_COME @EAST_FINGER OUT,@EAST_SUM OUT'
			SET @EAST_TIMESTART=GETDATE()
			EXEC SP_EXECUTESQL   
				@EAST_COME,   
				N'@EAST_FINGER INT  OUTPUT,@EAST_SUM INT OUTPUT',
				@EAST_FINGER=@EAST_FINGER OUTPUT,  @EAST_SUM=@EAST_SUM OUTPUT
			SET @EAST_TIME=DATEDIFF(MS,@EAST_TIMESTART,GETDATE())
			--西家酒鬼出拳			
			SET @WEST_COME='EXEC '+@WESTID+'_COME @WEST_FINGER OUT,@WEST_SUM OUT'
			SET @WEST_TIMESTART=GETDATE()
			EXEC SP_EXECUTESQL   
				@WEST_COME,   
				N'@WEST_FINGER INT  OUTPUT,@WEST_SUM INT OUTPUT',
				@WEST_FINGER=@WEST_FINGER OUTPUT,  @WEST_SUM=@WEST_SUM OUTPUT
			SET @WEST_TIME=DATEDIFF(MS,@WEST_TIMESTART,GETDATE())
			--超时判断
			IF(@EAST_TIME>2000) 
				SET @EAST_TIMECOUNT=999
			ELSE
			IF(@EAST_TIME>300) 
				SET @EAST_TIMECOUNT=@EAST_TIMECOUNT+1
			IF(@WEST_TIME>2000) 
				SET @WEST_TIMECOUNT=999
			ELSE
			IF(@WEST_TIME>300) 
				SET @WEST_TIMECOUNT=@WEST_TIMECOUNT+1
			
			IF(@EAST_TIMECOUNT>=10 OR @WEST_TIMECOUNT>=10)
			BEGIN
				SET @BREAK_J=1 SET @BREAK_I=1
			END
			--犯规判断
			IF(@EAST_FINGER>5 OR @EAST_FINGER<0 OR @EAST_SUM>10 OR @EAST_SUM<0 OR 
				@EAST_SUM-@EAST_FINGER>5 OR @EAST_SUM<@EAST_FINGER OR @EAST_FINGER IS NULL OR @EAST_SUM IS NULL)
				SET @EAST_RESULT='犯规'
			IF(@WEST_FINGER>5 OR @WEST_FINGER<0 OR @WEST_SUM>10 OR @WEST_SUM<0 OR 
				@WEST_SUM-@WEST_FINGER>5 OR @WEST_SUM<@WEST_FINGER OR @WEST_FINGER IS NULL OR @WEST_SUM IS NULL)
				SET @WEST_RESULT='犯规'
			--结果判断
			IF(@EAST_FINGER+@WEST_FINGER=@EAST_SUM)
			BEGIN
				SET @EAST_RESULT='胜'
			END
			IF(@EAST_FINGER+@WEST_FINGER=@WEST_SUM)
			BEGIN
				SET @WEST_RESULT='胜'
			END
			IF(@EAST_RESULT=@WEST_RESULT)
			BEGIN
				SET @EAST_RESULT='平'
				SET @WEST_RESULT='平'
			END
			ELSE
			BEGIN
				IF(@EAST_RESULT='犯规')SET @WEST_RESULT='胜' 
				IF(@EAST_RESULT='胜')SET @WEST_RESULT='负'
				IF(@WEST_RESULT='犯规')SET @EAST_RESULT='胜' 
				IF(@WEST_RESULT='胜')SET @EAST_RESULT='负'
				SET @BREAK_J=1
			END
			IF(@EAST_RESULT='胜') SET @EAST_SCORE=@EAST_SCORE+1
			IF(@WEST_RESULT='胜') SET @WEST_SCORE=@WEST_SCORE+1
			
			--输出结果

			SET @EAST_COMEOUT='EXEC '+@EASTID+
				'_COMEOUT @EAST_FINGER,@EAST_SUM,@EAST_RESULT,@WEST_FINGER,@WEST_SUM,@WEST_RESULT'
			SET @EAST_TIMESTART=GETDATE()
			EXEC SP_EXECUTESQL   
				@EAST_COMEOUT,   
				N'@EAST_FINGER INT ,@EAST_SUM INT ,@EAST_RESULT VARCHAR(10),
				@WEST_FINGER INT,@WEST_SUM INT,@WEST_RESULT  VARCHAR(10)',
				@EAST_FINGER=@EAST_FINGER,  @EAST_SUM=@EAST_SUM ,@EAST_RESULT=@EAST_RESULT,
				@WEST_FINGER=@WEST_FINGER,  @WEST_SUM=@WEST_SUM ,@WEST_RESULT=@WEST_RESULT
			SET @EAST_TIME=DATEDIFF(MS,@EAST_TIMESTART,GETDATE())
			 
			SET @WEST_TIMESTART=GETDATE()
			SET @WEST_COMEOUT='EXEC '+@WESTID+
				'_COMEOUT @WEST_FINGER,@WEST_SUM,@WEST_RESULT,@EAST_FINGER,@EAST_SUM,@EAST_RESULT'
			EXEC SP_EXECUTESQL   
				@WEST_COMEOUT,   
				N'@WEST_FINGER INT,@WEST_SUM INT,@WEST_RESULT  VARCHAR(10),
				@EAST_FINGER INT ,@EAST_SUM INT ,@EAST_RESULT VARCHAR(10)',
				@EAST_FINGER=@EAST_FINGER,  @EAST_SUM=@EAST_SUM ,@EAST_RESULT=@EAST_RESULT,
				@WEST_FINGER=@WEST_FINGER,  @WEST_SUM=@WEST_SUM ,@WEST_RESULT=@WEST_RESULT
			SET @WEST_TIME=DATEDIFF(MS,@WEST_TIMESTART,GETDATE())
			
			--输出超时判断
			IF(@EAST_TIME>2000) 
				SET @EAST_TIMECOUNT=999
			ELSE
			IF(@EAST_TIME>300) 
				SET @EAST_TIMECOUNT=@EAST_TIMECOUNT+1
			IF(@WEST_TIME>2000) 
				SET @WEST_TIMECOUNT=999
			ELSE
			IF(@WEST_TIME>300) 
				SET @WEST_TIMECOUNT=@WEST_TIMECOUNT+1
			
			IF(@EAST_TIMECOUNT>=10 OR @WEST_TIMECOUNT>=10)
			BEGIN
				SET @BREAK_J=1 SET @BREAK_I=1
			END
			SET @J=@J+1

		END
		SET @I=@I+1
	END
	--输出最终结果
	IF (@EAST_TIME>1000)
		PRINT @EASTID+'初始化严重超时'
	IF (@WEST_TIME>1000)
		PRINT @WESTID+'初始化严重超时'
	IF(@EAST_TIMECOUNT=999)
		PRINT @EASTID+'严重超时'
	IF(@EAST_TIMECOUNT=10)
		PRINT @EASTID+'超时达到10次'
	IF(@WEST_TIMECOUNT=999)
		PRINT @WESTID+'严重超时'
	IF(@WEST_TIMECOUNT=10)
		PRINT @WESTID+'超时达到10次'
	IF(@BREAK_I=0)
		PRINT @EASTID+'分数:'+CAST( @EAST_SCORE AS VARCHAR(100))+' VS '+ @WESTID+'分数:'+CAST( @WEST_SCORE AS VARCHAR(100))
END

GO